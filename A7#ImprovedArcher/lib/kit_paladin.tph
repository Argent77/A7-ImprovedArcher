// Installing Paladin -> Bow Knight kit
INCLUDE ~A7#ImprovedArcher/lib/a7#add_kit_ex.tpa~
INCLUDE ~A7#ImprovedArcher/lib/kit.tph~

ACTION_IF (GAME_IS ~tob bgt bg2ee eet~) BEGIN
  OUTER_TEXT_SPRINT stweap ~LEAT14 * HELM07 BAG26 RING06 RING31 CLCK02 BOOT01 AMUL20 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 BOW18 XBOW09 STAF08~
END ELSE ACTION_IF (GAME_IS ~iwdee~) BEGIN
  OUTER_TEXT_SPRINT stweap ~USLTR4A * HELMPAT * URING3A * * * * * BELT03 AROW11,40 BULL03,40 BOLT06,40 POTN05,2 POTN20,5 POTN08,5 BLCKBOW ULXBW3B UBSWD4A~
END ELSE BEGIN
  OUTER_TEXT_SPRINT stweap ~* * * * * * * * * * * * * * * * * * * *~
END

LAF ADD_KIT_EX
INT_VAR
  kit_class = 6
  mixed         = RESOLVE_STR_REF(@1302)
  lower         = RESOLVE_STR_REF(@1301)
  help          = RESOLVE_STR_REF(@1300)
  fallen_notice = RESOLVE_STR_REF(@1305)  // Lost Class: Bow Knight
STR_VAR
  kit_name  = ~A7_BOWKNIGHT~
  unusable  = ~0x00008000~
  clasweap  = ~1 1 1 1 1 1 1 1~
  weapprof  = ~1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 5 5 5 2 5 1 1 1 1 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
  abclasrq  = ~12 0 9 0 13 17~
  abclsmod  = ~0 0 0 0 0 0~
  abdcdsrq  = ~17 0 0 0 0 17~
  abdcscrq  = ~15 0 0 0 0 15~
  alignmnt  = ~1 0 0 0 0 0 0 0 0~
  dualclas  = ~0 0 0 0 0 0~
  clab_path = EVAL ~A7#ImprovedArcher/kit/%CLAB_DIR%/CLABA7P1.2DA~
  kittable  = ~K_P_H~
  luabbr    = ~A7pa1~
  stweap
  bdstweap  = ~LEAT05 * HELM01 * RING06 * * * * * BELT02 AROW02,40 BULL02,40 BOLT02,40 POTN08,5 POTN17,3 POTN03,1 BOW06 XBOW05 SW2H02~
  clsrcreq  = ~1 0 0 0 0 0 0~
  clswpbon  = ~1 0 2~
  hpclass   = ~HPWAR~
  numwslot  = ~3~
  clascolr  = ~25 28 57 2 24~
RET
  kit_id
END

// Creating "Fallen" kit variant
ACTION_IF (GAME_IS ~bgee bg2ee eet iwdee~) BEGIN
  // collecting CLASTEXT resources
  ACTION_CLEAR_ARRAY ~clastext~
  OUTER_SET $clastext(~CLASTEXT~) = 1
  COPY_EXISTING - ~CAMPAIGN.2DA~ ~override~
    COUNT_2DA_COLS num_cols
    PATCH_IF (num_cols > 30) BEGIN
      READ_2DA_ENTRY 1 0 1 def_value
      COUNT_2DA_ROWS 1 num_rows
      FOR (row = 3; row < num_rows; ++row) BEGIN
        READ_2DA_ENTRY row 30 1 resref
        PATCH_IF (NOT ~%resref%~ STR_EQ ~%def_value%~ && FILE_EXISTS_IN_GAME ~%resref%.2DA~) BEGIN
          TO_UPPER ~resref~
          SET $clastext(~%resref%~) = 1
        END
      END
    END
  BUT_ONLY IF_EXISTS

  // Adding "fallen" line
  OUTER_SET lower_strref = RESOLVE_STR_REF(@1303) // fallen bow knight
  OUTER_SET mixed_strref = RESOLVE_STR_REF(@1304) // Fallen Bow Knight
  ACTION_PHP_EACH clastext AS resref => defined BEGIN
    ACTION_IF (IS_AN_INT ~defined~ && defined) BEGIN
      COPY_EXISTING ~%resref%.2DA~ ~override~
        SET p1 = INDEX_BUFFER(~^A7_BOWKNIGHT~)
        PATCH_IF (p1 >= 0) BEGIN
          SET p2 = INDEX_BUFFER(~[%LNL%]~ p1) + 1
          PATCH_IF (p2 > p1) BEGIN
            READ_ASCII p1 line (p2 - p1)
            INNER_PATCH_SAVE line ~%line%~ BEGIN
              COUNT_2DA_COLS num_cols
              SET_2DA_ENTRY 0 0 num_cols ~A7_FALLEN_BOWKNIGHT~
              SET_2DA_ENTRY 0 3 num_cols ~%lower_strref%~ // LOWER
              SET_2DA_ENTRY 0 5 num_cols ~%mixed_strref%~ // MIXED
              SET_2DA_ENTRY 0 7 num_cols ~1~  // FALLEN
              PATCH_IF (num_cols > 9) BEGIN
                SET_2DA_ENTRY 0 9 num_cols ~-1~  // FALLEN_NOTICE
              END
            END
            SET len = STRING_LENGTH ~%line%~
            INSERT_BYTES p1 len
            WRITE_ASCIIE p1 ~%line%~ (len)
          END
        END
        PRETTY_PRINT_2DA
      IF_EXISTS
    END
  END
END

COPY ~A7#ImprovedArcher/kit/LUA7PA1.2DA~ ~override~

COPY_EXISTING ~CLABA7P1.2DA~ ~override~
  PATCH_IF (GAME_IS ~tob bgt~) BEGIN
    LPF CLAB_FIX_ENTRY END
  END
  BUT_ONLY
