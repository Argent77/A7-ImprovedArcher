// Installing Thief -> Sharpshooter kit
INCLUDE ~A7#ImprovedArcher/lib/a7#add_kit_ex.tpa~
INCLUDE ~A7#ImprovedArcher/lib/kit.tph~

ACTION_IF (GAME_IS ~tob bgt bg2ee eet~) BEGIN
  OUTER_TEXT_SPRINT stweap ~LEAT14 * * BAG28 RING06 RING05 * BOOT02 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 DAGG12 SW1H28 STAF08~
END ELSE ACTION_IF (GAME_IS ~iwdee~) BEGIN
  OUTER_TEXT_SPRINT stweap ~USLTR3A * * * SHADOWS URING5B * SHBOOTS BWTALIS KAYGLOV * AROW11,40 BULL03,40 BOLT06,40 FIREKIS POTN36,2 POTN08,5 BOW06 DART04,40 USSWD4A~
END ELSE BEGIN
  OUTER_TEXT_SPRINT stweap ~* * * * * * * * * * * * * * * * * * * *~
END

// TODO: Change English kit name (Sharpshooter) to something else: Conflicts with kit in "Song & Silence".
//       Options: Sniper (used by other mods), Ninja (likely used by other mods as well), Silencer, Ambusher, ???
LAF ADD_KIT_EX
INT_VAR
  kit_class = 4
  mixed     = RESOLVE_STR_REF(@1402)
  lower     = RESOLVE_STR_REF(@1401)
  help      = RESOLVE_STR_REF(@1400)
STR_VAR
  kit_name  = ~A7_SHARPSHOOTER~
  unusable  = ~0x00040000~
  clasweap  = ~1 1 1 1 1 0 0 0~
  weapprof  = ~0 1 0 0 1 0 0 1 0 1 1 0 0 1 1 1 0 1 0 0 0 0 1 5 0 5 5 5 1 1 1 1 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
  abclasrq  = ~0 9 0 0 0 0~
  abclsmod  = ~0 0 0 0 0 0~
  abdcdsrq  = ~0 17 0 0 0 0~
  abdcscrq  = ~0 15 0 0 0 0~
  alignmnt  = ~0 1 1 1 1 1 1 1 1~
  dualclas  = ~1 1 1 0 0 0~
  clab_path = EVAL ~A7#ImprovedArcher/kit/%CLAB_DIR%/CLABA7T1.2DA~
  kittable  = ~K_T_H K_T_D K_T_G K_T_E K_T_HE K_T_HL K_T_HO~
  luabbr    = ~A7th1~
  stweap
  bdstweap  = ~LEAT05 * * * * * * * * BRAC09 BELT02 AROW02,40 BULL02,40 BOLT02,40 POTN08,5 POTN17,3 POTN36,1 * BOW06 DAGG02~
  clsrcreq  = ~1 1 1 1 1 1 1~
  clswpbon  = ~0 0 3~
  hpclass   = ~HPROG~
  numwslot  = ~2~
  clascolr  = ~24 99 169 2 93~
  backstab  = ~1~
  sneakatt  = ~1~
  crippstr  = ~0~
  thiefskl  = ~40 15~
RET
  kit_id
END

COPY ~A7#ImprovedArcher/kit/LUA7TH1.2DA~ ~override~

COPY_EXISTING ~CLABA7T1.2DA~ ~override~
  PATCH_IF (GAME_IS ~tob bgt~) BEGIN
    LPF CLAB_FIX_ENTRY END
  END
  BUT_ONLY
