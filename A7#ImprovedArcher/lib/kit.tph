// Installing Ranger -> Archer kit

// Defines a work-around for a buggy implementation of opcode 122 (Create inventory item) in the Enhanced Editions patch v1.x
ACTION_IF (GAME_IS ~iwdee~) BEGIN
  OUTER_TEXT_SPRINT spl_arrow "A7#ImprovedArcher/kit/ee/A7#IN04A.SPL"
  OUTER_TEXT_SPRINT spl_bolt  "A7#ImprovedArcher/kit/ee/A7#IN04B.SPL"
END ELSE BEGIN
  OUTER_TEXT_SPRINT spl_arrow "A7#ImprovedArcher/kit/A7#IN04A.SPL"
  OUTER_TEXT_SPRINT spl_bolt  "A7#ImprovedArcher/kit/A7#IN04B.SPL"
END
OUTER_SET help_strref = RESOLVE_STR_REF(@1100)    // Archer kit description

// Updating Archer description and ability table
OUTER_SET kit_id = "-1"
COPY_EXISTING ~KITLIST.2DA~ ~override~
  COUNT_2DA_COLS numCols
  COUNT_2DA_ROWS numCols numRows
  SET found = 0
  FOR (row = 0; row < numRows && found < 1; row += 1) BEGIN
    READ_2DA_ENTRY row 1 numCols value
    PATCH_IF (~%value%~ STRING_EQUAL_CASE ~FERALAN~ || ~%value%~ STRING_EQUAL_CASE ~ARCHER~) BEGIN
      READ_2DA_ENTRY row 0 numCols kit_id
      SET_2DA_ENTRY row 4 numCols ~%help_strref%~
      SET_2DA_ENTRY row 5 numCols ~CLABA7R1~
      found = 1
    END
  END
BUT_ONLY

// Limiting weapon styles
COPY_EXISTING ~WEAPPROF.2DA~ ~override~
  COUNT_2DA_COLS numCols
  COUNT_2DA_ROWS numCols numRows
  SET found = 0
  FOR (row = 0; row < numRows && found < 4; row += 1) BEGIN
    READ_2DA_ENTRY row 0 numCols value
    PATCH_IF (~%value%~ STRING_EQUAL_CASE ~2HANDED~) BEGIN
      SET_2DA_ENTRY row 36 numCols ~1~
      found += 1
    END ELSE PATCH_IF (~%value%~ STRING_EQUAL_CASE ~SWORDANDSHIELD~) BEGIN
      SET_2DA_ENTRY row 36 numCols ~1~
      found += 1
    END ELSE PATCH_IF (~%value%~ STRING_EQUAL_CASE ~SINGLEWEAPON~) BEGIN
      SET_2DA_ENTRY row 36 numCols ~1~
      found += 1
    END ELSE PATCH_IF (~%value%~ STRING_EQUAL_CASE ~2WEAPON~) BEGIN
      SET_2DA_ENTRY row 36 numCols ~2~
      found += 1
    END
  END
BUT_ONLY

// Modifying DEX and CON requirements
COPY_EXISTING ~ABCLASRQ.2DA~ ~override~
  COUNT_2DA_COLS numCols
  COUNT_2DA_ROWS numCols numRows
  SET found = 0
  FOR (row = 0; row < numRows && found < 1; row += 1) BEGIN
    READ_2DA_ENTRY row 0 numCols value
    PATCH_IF (~%value%~ STRING_EQUAL_CASE ~FERALAN~) BEGIN
      SET_2DA_ENTRY row 2 numCols ~14~
      SET_2DA_ENTRY row 3 numCols ~13~
      found = 1
    END
  END
BUT_ONLY

// Setting CON penalty
COPY_EXISTING ~ABCLSMOD.2DA~ ~override~
  COUNT_2DA_COLS numCols
  COUNT_2DA_ROWS numCols numRows
  SET found = 0
  FOR (row = 0; row < numRows && found < 1; row += 1) BEGIN
    READ_2DA_ENTRY row 0 numCols value
    PATCH_IF (~%value%~ STRING_EQUAL_CASE ~FERALAN~) BEGIN
      SET_2DA_ENTRY row 3 numCols ~-1~
      found = 1
    END
  END
BUT_ONLY

// Setting new HLA table
COPY_EXISTING ~LUABBR.2DA~ ~override~
  COUNT_2DA_COLS numCols
  COUNT_2DA_ROWS numCols numRows
  SET found = 0
  FOR (row = 0; row < numRows && found < 1; row += 1) BEGIN
    READ_2DA_ENTRY row 0 numCols value
    PATCH_IF (~%value%~ STRING_EQUAL_CASE ~FERALAN~) BEGIN
      SET_2DA_ENTRY row 1 numCols ~A7ra1~
      found = 1
    END
  END
BUT_ONLY

ACTION_IF (GAME_IS ~bgee bg2ee eet iwdee~) BEGIN
  // Updating Archer description
  COPY_EXISTING ~CLASTEXT.2DA~ ~override~
    COUNT_2DA_COLS numCols
    COUNT_2DA_ROWS numCols numRows
    SET found = 0
    FOR (row = 0; row < numRows && found < 2; row += 1) BEGIN
      READ_2DA_ENTRY row 2 numCols id
      PATCH_IF (kit_id >= 0 && id = kit_id) BEGIN
        SET_2DA_ENTRY row 4 numCols ~%help_strref%~
        found += 1
      END
    END
  BUT_ONLY

  // Handling SoD class text
  INCLUDE ~A7#ImprovedArcher/lib/campaigns.tph~
  LAF CAMPAIGN_EXISTS RET exists END
  ACTION_IF (exists != 0) BEGIN
    LAF GET_CAMPAIGN_INFO RET cmp_clastext END
    OUTER_TEXT_SPRINT clastext_file ~%cmp_clastext%.2DA~
    ACTION_IF (FILE_EXISTS_IN_GAME ~%clastext_file%~) BEGIN
      COPY_EXISTING ~%clastext_file%~ ~override~
        COUNT_2DA_COLS numCols
        COUNT_2DA_ROWS numCols numRows
        SET found = 0
        FOR (row = 0; row < numRows && found < 2; row += 1) BEGIN
          READ_2DA_ENTRY row 2 numCols id
          PATCH_IF (kit_id >= 0 && id = kit_id) BEGIN
            SET_2DA_ENTRY row 4 numCols ~%help_strref%~
            found += 1
          END
        END
      BUT_ONLY
    END
  END

  // In-game help
  ACTION_IF (FILE_EXISTS_IN_GAME ~ENGINEST.2DA~) BEGIN
    COPY_EXISTING ~ENGINEST.2DA~ ~override~
      COUNT_2DA_COLS numCols
      COUNT_2DA_ROWS numCols numRows
      SET found = 0
      FOR (row = 0; row < numRows && found < 2; row += 1) BEGIN
        READ_2DA_ENTRY row 0 numCols value
        PATCH_IF (~%value%~ STRING_EQUAL_CASE ~STRREF_GUI_HELP_KIT_FERALAN~) BEGIN
          SET_2DA_ENTRY row 1 numCols ~%help_strref%~
        END
      END
    BUT_ONLY
  END

  COPY ~A7#ImprovedArcher/kit/ee/A7#IARO1.BAM~ ~override~
       ~A7#ImprovedArcher/kit/ee/A7#IBLT1.BAM~ ~override~
       ~A7#ImprovedArcher/kit/ee/A7#IN01B.BAM~ ~override~
       ~A7#ImprovedArcher/kit/ee/A7#IN02B.BAM~ ~override~
       ~A7#ImprovedArcher/kit/ee/A7#IN03B.BAM~ ~override~
       ~A7#ImprovedArcher/kit/ee/A7#IN04B.BAM~ ~override~
       ~A7#ImprovedArcher/kit/ee/A7#IN4AB.BAM~ ~override~
       ~A7#ImprovedArcher/kit/ee/A7#IN4BB.BAM~ ~override~
       ~A7#ImprovedArcher/kit/ee/A7#IN0AB.BAM~ ~override~
       ~A7#ImprovedArcher/kit/ee/A7#IN0BB.SPL~ ~override~
END ELSE BEGIN
  COPY ~A7#ImprovedArcher/kit/A7#IARO1.BAM~ ~override~
       ~A7#ImprovedArcher/kit/A7#IBLT1.BAM~ ~override~
       ~A7#ImprovedArcher/kit/A7#IN01B.BAM~ ~override~
       ~A7#ImprovedArcher/kit/A7#IN02B.BAM~ ~override~
       ~A7#ImprovedArcher/kit/A7#IN03B.BAM~ ~override~
       ~A7#ImprovedArcher/kit/A7#IN04B.BAM~ ~override~
       ~A7#ImprovedArcher/kit/A7#IN4AB.BAM~ ~override~
       ~A7#ImprovedArcher/kit/A7#IN4BB.BAM~ ~override~
       ~A7#ImprovedArcher/kit/A7#IN0AB.BAM~ ~override~
       ~A7#ImprovedArcher/kit/A7#IN0BB.SPL~ ~override~
END

ACTION_IF (GAME_IS ~iwdee~) BEGIN
  COPY ~A7#ImprovedArcher/kit/CLABA7R1_iwd.2DA~ ~override/CLABA7R1.2DA~
END ELSE BEGIN
  COPY ~A7#ImprovedArcher/kit/CLABA7R1_bg.2DA~ ~override/CLABA7R1.2DA~
END

COPY ~A7#ImprovedArcher/kit/A7#IN04.2DA~  ~override~
     ~A7#ImprovedArcher/kit/LUA7RA1.2DA~  ~override~
     ~A7#ImprovedArcher/kit/A7#IN11A.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN11B.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN11C.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN12A.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN12B.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN12C.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN13A.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN13B.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN13C.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN14A.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN14B.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN14C.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN21A.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN21B.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN21C.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN21D.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN21E.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN21F.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN22A.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN22B.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN22C.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN22D.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN22E.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN22F.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN23A.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN23B.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN23C.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN23D.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN23E.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN23F.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN24A.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN24B.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN24C.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN24D.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN24E.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN24F.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN25A.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN25B.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN25C.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN25D.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN25E.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN25F.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN31A.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN32A.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN33A.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN34A.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN41A.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN41B.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN42A.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN42B.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN43A.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN43B.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN44A.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN44B.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN45A.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN45B.EFF~ ~override~
     ~A7#ImprovedArcher/kit/A7#IN03.PRO~  ~override~
     ~A7#ImprovedArcher/kit/A7#IN05.SPL~  ~override~
     ~A7#ImprovedArcher/kit/A7#ENTG.BAM~  ~override~
     ~A7#ImprovedArcher/kit/A7#ENTG1.VVC~ ~override~

// Initializing user-defined status icons for shot abilities (EE-only)
ACTION_IF (GAME_IS ~bgee bg2ee eet~) BEGIN
  COPY_EXISTING ~STATDESC.2DA~ ~override~
    SET icon_index = 82
    COUNT_2DA_COLS numCols
    PATCH_IF (numCols > 2) BEGIN
      READ_2DA_ENTRIES_NOW ~tbl_items~ numCols
      FOR (row = 0; row < tbl_items; ++row) BEGIN
        READ_2DA_ENTRY_FORMER ~tbl_items~ row 0 cur_idx
        PATCH_IF (cur_idx > icon_index) BEGIN
          SET icon_index = cur_idx
        END
      END
      PATCH_IF (icon_index > 82) BEGIN
        // Note: Adding rows in reverse order since 2DA functions don't update max. rows internally
        DEFINE_ASSOCIATIVE_ARRAY icon_defs BEGIN 
          1017 => ~A7#IN0AD~
          1002 => ~A7#IN03D~
          1001 => ~A7#IN02D~
          1000 => ~A7#IN01D~
        END
        SET cur_index = icon_index + 4
        PHP_EACH icon_defs AS ref => icn_name BEGIN
          SET cur_strref = RESOLVE_STR_REF((AT ref))
          TEXT_SPRINT icon_row ~%cur_index%        %cur_strref%          %icn_name%~
          INSERT_2DA_ROW tbl_items numCols ~%icon_row%~
          SET cur_index -= 1
        END
        INNER_ACTION BEGIN
          COPY ~A7#ImprovedArcher/kit/ee/A7#IN01D.BAM~ ~override~
          COPY ~A7#ImprovedArcher/kit/ee/A7#IN02D.BAM~ ~override~
          COPY ~A7#ImprovedArcher/kit/ee/A7#IN03D.BAM~ ~override~
          COPY ~A7#ImprovedArcher/kit/ee/A7#IN0AD.BAM~ ~override~
        END
      END
    END
END ELSE BEGIN
  OUTER_SET icon_index = 82
END

// Ability: Power Shot
COPY ~A7#ImprovedArcher/kit/A7#IN01.SPL~ ~override~
  SAY NAME1 @1000   // Power Shot
  PATCH_IF (GAME_IS ~bgee bg2ee eet iwdee~) BEGIN
    SAY UNIDENTIFIED_DESC @2001   // Power Shot...
    PATCH_IF (icon_index != 82) BEGIN
      LPF ALTER_EFFECT
      INT_VAR
        check_globals     = 0
        check_headers     = 1
        match_opcode      = 142   // Display portrait icon
        match_parameter2  = 82    // Called Shot icon
        parameter2        = (icon_index + 1)
      END
    END
  END


// Ability: Rooting Shot
COPY ~A7#ImprovedArcher/kit/A7#IN02.SPL~ ~override~
  SAY NAME1 @1001   // Rooting Shot
  PATCH_IF (GAME_IS ~bgee bg2ee eet iwdee~) BEGIN
    SAY UNIDENTIFIED_DESC @2002   // Rooting Shot...
    PATCH_IF (icon_index != 82) BEGIN
      LPF ALTER_EFFECT
      INT_VAR
        check_globals     = 0
        check_headers     = 1
        match_opcode      = 142   // Display portrait icon
        match_parameter2  = 82    // Called Shot icon
        parameter2        = (icon_index + 2)
      END
    END
  END


// Ability: Explosive Shot
ADD_PROJECTILE ~A7#ImprovedArcher/kit/A7#IN03.PRO~
COPY ~A7#ImprovedArcher/kit/A7#IN03.SPL~ ~override~
  SAY NAME1 @1002   // Explosive Shot
  PATCH_IF (GAME_IS ~bgee bg2ee eet iwdee~) BEGIN
    SAY UNIDENTIFIED_DESC @2003   // Explosive Shot...
    PATCH_IF (icon_index != 82) BEGIN
      LPF ALTER_EFFECT
      INT_VAR
        check_globals     = 0
        check_headers     = 1
        match_opcode      = 142   // Display portrait icon
        match_parameter2  = 82    // Called Shot icon
        parameter2        = (icon_index + 3)
      END
    END
  END
COPY ~A7#ImprovedArcher/kit/A7#IN31A.SPL~  ~override~
     ~A7#ImprovedArcher/kit/A7#IN32A.SPL~  ~override~
     ~A7#ImprovedArcher/kit/A7#IN33A.SPL~  ~override~
     ~A7#ImprovedArcher/kit/A7#IN34A.SPL~  ~override~
  SAY NAME1 @1002   // Explosive Shot
  READ_LONG 0x64 "ability_ofs"
  READ_SHORT 0x68 "ability_num"
  FOR (ability = 0; ability < ability_num; ability += 1) BEGIN
    WRITE_SHORT (ability_ofs + ability*0x28 + 0x26) %A7#IN03%
  END


// Setting common shot ability properties
COPY_EXISTING ~A7#IN01.SPL~ ~override~
              ~A7#IN02.SPL~ ~override~
              ~A7#IN03.SPL~ ~override~
  READ_LONG 0x6a "effects_ofs"
  WHILE (effects_ofs < SOURCE_SIZE) BEGIN
    READ_SHORT effects_ofs "fx_type"
    PATCH_IF (fx_type == 206) BEGIN   // Protection from spell (206)
      SAY (effects_ofs + 4) @1016   // You can't use another shot ability while the current effect is active.
    END
    effects_ofs += 0x30
  END

// Ability: Create Blessed Ammunition
COPY ~A7#ImprovedArcher/kit/A7#IN04.SPL~ ~override~
  SAY NAME1 @1003   // Create Blessed Ammunition
  PATCH_IF (GAME_IS ~bgee bg2ee eet iwdee~) BEGIN
    SAY UNIDENTIFIED_DESC @2004   // Create Spiritual Ammunition...
  END

COPY ~%spl_arrow%~ ~override~
  SAY NAME1 @1004   // Create Blessed Arrows
  PATCH_IF (GAME_IS ~bgee bg2ee eet iwdee~) BEGIN
    SAY UNIDENTIFIED_DESC @2005   // Create Spiritual Arrows...
  END
  READ_LONG 0x6a "effects_ofs"
  WHILE (effects_ofs < SOURCE_SIZE) BEGIN
    READ_SHORT effects_ofs "fx_type"
    PATCH_IF (fx_type == 139) BEGIN   // Display string (139)
      SAY (effects_ofs + 4) @1014   // Blessed Arrows created
    END
    effects_ofs += 0x30
  END

COPY ~%spl_bolt%~ ~override~
  SAY NAME1 @1005   // Create Blessed Bolts
  PATCH_IF (GAME_IS ~bgee bg2ee eet iwdee~) BEGIN
    SAY UNIDENTIFIED_DESC @2006   // Create Spiritual Bolts...
  END
  READ_LONG 0x6a "effects_ofs"
  WHILE (effects_ofs < SOURCE_SIZE) BEGIN
    READ_SHORT effects_ofs "fx_type"
    PATCH_IF (fx_type == 139) BEGIN   // Display string (139)
      SAY (effects_ofs + 4) @1015   // Blessed Bolts created
    END
    effects_ofs += 0x30
  END

// Blessed Arrows and Bolts
COPY ~A7#ImprovedArcher/kit/A7#INARA.ITM~ ~override~
  SAY NAME2 @1006   // Blessed Arrow +1
  SAY IDENTIFIED_DESC @1101   // This is an arrow that has been blessed by the Archer's worshipped god or goddess...
  PATCH_IF (GAME_IS ~iwdee~) BEGIN
    WRITE_ASCII 0x58 ~~ #8    // Description image
  END

COPY ~A7#ImprovedArcher/kit/A7#INARB.ITM~ ~override~
  SAY NAME2 @1006   // Blessed Arrow +1
  SAY IDENTIFIED_DESC @1102   // This is an arrow that has been blessed by the Archer's worshipped god or goddess...
  PATCH_IF (GAME_IS ~iwdee~) BEGIN
    WRITE_ASCII 0x58 ~~ #8    // Description image
  END

COPY ~A7#ImprovedArcher/kit/A7#INARC.ITM~ ~override~
  SAY NAME2 @1007   // Blessed Arrow +2
  SAY IDENTIFIED_DESC @1103   // This is an arrow that has been blessed by the Archer's worshipped god or goddess...
  PATCH_IF (GAME_IS ~iwdee~) BEGIN
    WRITE_ASCII 0x58 ~~ #8    // Description image
  END

COPY ~A7#ImprovedArcher/kit/A7#INARD.ITM~ ~override~
  SAY NAME2 @1008   // Blessed Arrow +3
  SAY IDENTIFIED_DESC @1104   // This is an arrow that has been blessed by the Archer's worshipped god or goddess...
  PATCH_IF (GAME_IS ~iwdee~) BEGIN
    WRITE_ASCII 0x58 ~~ #8    // Description image
  END

COPY ~A7#ImprovedArcher/kit/A7#INARE.ITM~ ~override~
  SAY NAME2 @1009   // Blessed Arrow +4
  SAY IDENTIFIED_DESC @1105   // This is an arrow that has been blessed by the Archer's worshipped god or goddess...
  PATCH_IF (GAME_IS ~iwdee~) BEGIN
    WRITE_ASCII 0x58 ~~ #8    // Description image
  END

COPY ~A7#ImprovedArcher/kit/A7#INBTA.ITM~ ~override~
  SAY NAME2 @1010   // Blessed Bolt +1
  SAY IDENTIFIED_DESC @1106   // This is an bolt that has been blessed by the Archer's worshipped god or goddess...
  PATCH_IF (GAME_IS ~iwdee~) BEGIN
    WRITE_ASCII 0x58 ~~ #8    // Description image
  END

COPY ~A7#ImprovedArcher/kit/A7#INBTB.ITM~ ~override~
  SAY NAME2 @1010   // Blessed Bolt +1
  SAY IDENTIFIED_DESC @1107   // This is an bolt that has been blessed by the Archer's worshipped god or goddess...
  PATCH_IF (GAME_IS ~iwdee~) BEGIN
    WRITE_ASCII 0x58 ~~ #8    // Description image
  END

COPY ~A7#ImprovedArcher/kit/A7#INBTC.ITM~ ~override~
  SAY NAME2 @1011   // Blessed Bolt +2
  SAY IDENTIFIED_DESC @1108   // This is an bolt that has been blessed by the Archer's worshipped god or goddess...
  PATCH_IF (GAME_IS ~iwdee~) BEGIN
    WRITE_ASCII 0x58 ~~ #8    // Description image
  END

COPY ~A7#ImprovedArcher/kit/A7#INBTD.ITM~ ~override~
  SAY NAME2 @1012   // Blessed Bolt +3
  SAY IDENTIFIED_DESC @1109   // This is an bolt that has been blessed by the Archer's worshipped god or goddess...
  PATCH_IF (GAME_IS ~iwdee~) BEGIN
    WRITE_ASCII 0x58 ~~ #8    // Description image
  END

COPY ~A7#ImprovedArcher/kit/A7#INBTE.ITM~ ~override~
  SAY NAME2 @1013   // Blessed Bolt +4
  SAY IDENTIFIED_DESC @1110   // This is an bolt that has been blessed by the Archer's worshipped god or goddess...
  PATCH_IF (GAME_IS ~iwdee~) BEGIN
    WRITE_ASCII 0x58 ~~ #8    // Description image
  END


// Ability: Sure Shot
COPY ~A7#ImprovedArcher/kit/A7#IN0A.SPL~ ~override~
  SAY NAME1 @1017   // Sure Shot
  SAY UNIDENTIFIED_DESC @1111   // Sure Shot...
  PATCH_IF (GAME_IS ~bgee bg2ee eet iwdee~ AND icon_index != 82) BEGIN
    LPF ALTER_EFFECT
    INT_VAR
      check_globals     = 0
      check_headers     = 1
      match_opcode      = 142   // Display portrait icon
      match_parameter2  = 82    // Called Shot icon
      parameter2        = (icon_index + 4)
    END
  END


// Ability: Missile Trap
COPY ~A7#ImprovedArcher/kit/A7#IN0B.SPL~ ~override~
  SAY NAME1 @1018   // Missile Trap
  SAY UNIDENTIFIED_DESC @1112   // Set Missile Trap...

