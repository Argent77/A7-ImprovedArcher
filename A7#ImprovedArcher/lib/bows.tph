// Installing two powerful bows

ACTION_IF (IS_EE) BEGIN
  COPY ~%MOD_FOLDER%/kit/ee/a7#bow01.bam~ ~override~
       ~%MOD_FOLDER%/kit/ee/a7#bow02.bam~ ~override~
END ELSE BEGIN
  COPY ~%MOD_FOLDER%/kit/a7#bow01.bam~ ~override~
       ~%MOD_FOLDER%/kit/a7#bow02.bam~ ~override~
END

COPY ~%MOD_FOLDER%/kit/a7#bow01.itm~ ~override~
  SAY NAME2 @1021   // Great Sun-Reacher +4
  SAY IDENTIFIED_DESC @1117   // The elves of Evereska are known for their...
  PATCH_IF (GAME_IS ~iwdee~) BEGIN
    WRITE_LONG 0x34 16000     // Higher price in IWD
    WRITE_ASCII 0x58 ~~ #8    // Description image
  END

COPY ~%MOD_FOLDER%/kit/a7#bow02.itm~ ~override~
  SAY NAME2 @1022   // The Defender +5
  SAY IDENTIFIED_DESC @1118   // Composite longbows are bows whose staves are made from...
  PATCH_IF (GAME_IS ~iwdee~) BEGIN
    WRITE_LONG 0x34 9000      // Higher price in IWD
    WRITE_ASCII 0x58 ~~ #8    // Description image
  END

ACTION_IF (GAME_IS ~iwdee~) BEGIN
  COPY ~%MOD_FOLDER%/kit/a7#bow03.itm~ ~override~
    SAY NAME2 @1074   // Shortbow +2
    SAY IDENTIFIED_DESC @1160   // Shortbows were the first bows to be developed,...
    PATCH_IF (NOT IS_EE_V2) BEGIN
      LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 319 END
    END

  COPY ~%MOD_FOLDER%/kit/a7#bow04.itm~ ~override~
    SAY NAME2 @1075   // Shortbow +3
    SAY IDENTIFIED_DESC @1161   // Shortbows were the first bows to be developed,...
    PATCH_IF (NOT IS_EE_V2) BEGIN
      LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 319 END
    END

  COPY ~%MOD_FOLDER%/kit/a7#bow05.itm~ ~override~
    SAY NAME2 @1076   // Eye of the Hunter
    SAY IDENTIFIED_DESC @1162   // This small barbarian bow is foreign to the lands of Icewind Dale,...
    PATCH_IF (NOT IS_EE_V2) BEGIN
      LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 319 END
    END

  COPY ~%MOD_FOLDER%/kit/a7#bow06.itm~ ~override~
    SAY NAME2 @1077   // Shortbow of the Defender +2
    SAY IDENTIFIED_DESC @1163   // Shortbows were the first bows to be developed,...
    PATCH_IF (NOT IS_EE_V2) BEGIN
      LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 319 END
    END

  COPY ~%MOD_FOLDER%/kit/a7#bow07.itm~ ~override~
    SAY NAME2 @1078   // Shortbow of Action +2
    SAY IDENTIFIED_DESC @1164   // Shortbows were the first bows to be developed,...
    PATCH_IF (NOT IS_EE_V2) BEGIN
      LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 319 END
    END
END


// Adding longbow and shortbows
ACTION_IF (GAME_IS ~tob bgt bg2ee eet~) BEGIN
  COPY_EXISTING ~teltief2.cre~ ~override~
    ADD_CRE_ITEM ~A7#BOW01~ #0 #0 #0 ~none~ ~inv1 inv2 inv3 inv4 inv5 inv6 inv7 inv8 inv9 inv10 inv11 inv12 inv13 inv14 inv15 inv16~
    ADD_CRE_ITEM ~A7#AROW1~ #40 #0 #0 ~none~ ~quiver1 quiver2 quiver3 quiver4 inv1 inv2 inv3 inv4 inv5 inv6 inv7 inv8 inv9 inv10 inv11 inv12 inv13 inv14 inv15 inv16~
END ELSE ACTION_IF (GAME_IS ~iwdee~) BEGIN
  <<<<<<<< .../inlined/A7#ImprovedArcher/ar2100.baf
  IF
    Global("A7#BowAdded","AR2100",0)
    GlobalGT("Chapter","GLOBAL",1)
  THEN RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("A7#BowAdded","AR2100",1)
    AddStoreItem("KUSMITH","A7#BOW03",1,1)
    SetInterrupt(TRUE)
    Continue()
  END

  IF
    Global("A7#BowAdded","AR2100",1)
    GlobalGT("Chapter","GLOBAL",3)
  THEN RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("A7#BowAdded","AR2100",2)
    AddStoreItem("KUSMITH","A7#BOW01",1,1)
    AddStoreItem("KUSMITH","A7#BOW02",1,1)
    AddStoreItem("KUSMITH","A7#BOW04",1,1)
    AddStoreItem("KUSMITH","A7#AROW1",240,1)
    AddStoreItem("KUSMITH","A7#BOLT1",120,1)
    SetInterrupt(TRUE)
    Continue()
  END
  >>>>>>>>
  EXTEND_BOTTOM ~ar2100.bcs~ ~.../inlined/A7#ImprovedArcher/ar2100.baf~
END


// Adding composite longbow
ACTION_IF (MOD_IS_INSTALLED "setup-A7#ImprovedArcher.tp2" "100") BEGIN
  OUTER_TEXT_SPRINT A7ARROW ~A7#AROW1~
END ELSE BEGIN
  OUTER_TEXT_SPRINT A7ARROW ~AROW15~
END

ACTION_IF (GAME_IS ~tob bgt bg2ee eet~) BEGIN
  COPY_EXISTING ~ar6100.are~ ~override~
    LPF fj_are_structure
      INT_VAR
      fj_loc_x          = 1949
      fj_loc_y          = 234
      fj_type           = 8   // nonvisible
      fj_lock_diff      = 0
      fj_box_left       = 1945
      fj_box_top        = 171
      fj_box_right      = 2000
      fj_box_bottom     = 225
      fj_vertex_0       = 1948 + (220 << 16)
      fj_vertex_1       = 1965 + (225 << 16)
      fj_vertex_2       = 1979 + (220 << 16)
      fj_vertex_3       = 1993 + (203 << 16)
      fj_vertex_4       = 2000 + (185 << 16)
      fj_vertex_5       = 1967 + (171 << 16)
      fj_vertex_6       = 1951 + (190 << 16)
      fj_vertex_7       = 1945 + (204 << 16)
      STR_VAR
      fj_structure_type = ~container~
      fj_name           = ~A7#Grave1~
    END
    LPF fj_are_structure
      INT_VAR
      fj_con_itm_idx    = SHORT_AT 0x74 - 1
      fj_charge0        = 1
      STR_VAR
      fj_structure_type = ~itm~
      fj_name           = ~A7#BOW02~
    END
    LPF fj_are_structure
      INT_VAR
      fj_con_itm_idx    = SHORT_AT 0x74 - 1
      fj_charge0        = 40
      STR_VAR
      fj_structure_type = ~itm~
      fj_name           = EVALUATE_BUFFER ~%A7ARROW%~
    END
    LPF fj_are_structure
      INT_VAR
      fj_con_itm_idx    = SHORT_AT 0x74 - 1
      fj_charge0        = 20
      STR_VAR
      fj_structure_type = ~itm~
      fj_name           = ~AROW08~    // Arrows of Fire
    END
    LPF fj_are_structure
      INT_VAR
      fj_con_itm_idx    = SHORT_AT 0x74 - 1
      fj_charge0        = 280
      STR_VAR
      fj_structure_type = ~itm~
      fj_name           = ~MISC07~    // Gold
    END

    LPF fj_are_structure
      INT_VAR
      fj_loc_x          = 2035
      fj_loc_y          = 276
      fj_type           = 8   // nonvisible
      fj_lock_diff      = 0
      fj_box_left       = 2035
      fj_box_top        = 199
      fj_box_right      = 2095
      fj_box_bottom     = 266
      fj_vertex_0       = 2037 + (258 << 16)
      fj_vertex_1       = 2053 + (266 << 16)
      fj_vertex_2       = 2072 + (256 << 16)
      fj_vertex_3       = 2082 + (246 << 16)
      fj_vertex_4       = 2095 + (216 << 16)
      fj_vertex_5       = 2055 + (199 << 16)
      fj_vertex_6       = 2041 + (220 << 16)
      fj_vertex_7       = 2035 + (240 << 16)
      STR_VAR
      fj_structure_type = ~container~
      fj_name           = ~A7#Grave2~
    END
    LPF fj_are_structure
      INT_VAR
      fj_con_itm_idx    = SHORT_AT 0x74 - 1
      fj_charge0        = 2
      STR_VAR
      fj_structure_type = ~itm~
      fj_name           = ~POTN55~    // Potion of Superior Healing
    END
    LPF fj_are_structure
      INT_VAR
      fj_con_itm_idx    = SHORT_AT 0x74 - 1
      fj_charge0        = 1
      STR_VAR
      fj_structure_type = ~itm~
      fj_name           = ~MISC42~    // Diamond
    END
    LPF fj_are_structure
      INT_VAR
      fj_con_itm_idx    = SHORT_AT 0x74 - 1
      fj_charge0        = 65
      STR_VAR
      fj_structure_type = ~itm~
      fj_name           = ~MISC07~    // Gold
    END
END


// Adding IWD bows
ACTION_IF (GAME_IS ~iwdee~) BEGIN
  // Shortbow +2
  COPY_EXISTING ~ar9400.are~ ~override~
    LPF fj_are_structure
      INT_VAR
        fj_con_itm_idx = 1
      STR_VAR
        fj_structure_type = ~itm~
        fj_name = ~A7#BOW03~
    END
  BUT_ONLY

  // Shortbow +3
  COPY_EXISTING ~ar9714.are~ ~override~
    LPF fj_are_structure
      INT_VAR
        fj_con_itm_idx = 19
      STR_VAR
        fj_structure_type = ~itm~
        fj_name = ~A7#BOW04~
    END
  BUT_ONLY

  // Eye of the Hunter
  COPY_EXISTING ~emmeric.sto~ ~override~
    LPF ADD_STORE_ITEM_EX
      STR_VAR
        item_name = ~A7#BOW05~
        position = ~LAST~
        flags = ~identified~
    END
  BUT_ONLY

  // Shortbow of the Defender +2
  COPY_EXISTING ~ar5003.are~ ~override~
    LPF fj_are_structure
      INT_VAR
        fj_con_itm_idx = 0
      STR_VAR
        fj_structure_type = ~itm~
        fj_name = ~A7#BOW06~
    END
  BUT_ONLY

  // Shortbow of Action +2
  COPY_EXISTING ~kelly.cre~ ~override~
    ADD_CRE_ITEM ~A7#BOW07~ #0 #0 #0 ~none~ ~weapon3 weapon4 inv1 inv2 inv3 inv4 inv5 inv6 inv7 inv8 inv9 inv10 inv11 inv12 inv13 inv14 inv15 inv16~
  BUT_ONLY
END
